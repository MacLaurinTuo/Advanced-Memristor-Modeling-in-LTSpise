*LTSpice nonlinear drift modified library model of a titanium dioxide 
*memristor with a modified Biolek window with additional sinusoidal component
*connections: te - top electrode, be - bottom electrode, Y - additional
*electrode for measuring the state variable
.subckt A2 te be Y
*memristor model parameters
.params ron=100 roff=16e3 k=10e3 mm=3.12 Cint=1 m=1e-4 pp=2
*capacitor for integration of memristor state variable time derivative
Cint Y 0 1 IC=0.3
*additional parallel high-valued resistor for preventing convergence problems
Rad Y 0 10G
*memristor state variable
Gy 0 Y value={(k*V(te,be)*(1/(ron*(V(Y))+roff*(1-V(Y))))*(1/(mm+1))*(mm*pow((sin(pi*V(Y))),2)+(1-pow((V(Y)-stpp(V(Y),m)),(2*pp)))))}
*memristor conductance 
G1 te be value={V(te,be)*((1/(ron*(V(Y))+roff*(1-V(Y)))))}
* a step-like logistic function
.func stpp(x,p)={0.5*(1+(x/sqrt(pow(x,2)+p)))}
.ends A2
*
* Source: Mladenov, V., S. Kirilov. , A Nonlinear Drift Memristor Model with a Modified Biolek Window Function 
and Activation Threshold. Electronics 2017, 6(4), 77; doi: 10.3390/electronics6040077, pp. 1 â€“ 15.
