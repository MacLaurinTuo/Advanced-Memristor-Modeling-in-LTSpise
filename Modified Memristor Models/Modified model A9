* A simplified tantalum oxide memristor model
*connections: top electrode te, bottom electrode be, additional electrode Y
*for measuring the state variable
.subckt K9 te be Y
*parameters
.params yon=0.06 sigmap=4e-5 Cint=1 A=1e-10 sigmaoff=0.013
.params yoff=0.4 Gm=0.025 m=1e-4 beta=500 k1=0.0002359
.params k2=0.0002121 h1=0.897e-05 h2=0.0006531 h3=2.88e-05
* modeling the memristor current
G1 te be value={V(te,be)*(Gm*V(Y)+(1-V(Y))*(h1*(pow(V(te,be),4))+h2*(pow(V(te,be),2))+h3))}
Gy 0 Y value={((A*sinh(V(te,be)/sigmaoff)*exp(-pow(yoff/V(Y),2))*exp(1/(1+beta*I(G1)*V(te,be)))*stpp(-V(te,be),m))+(((k1*(pow(V(te,be),3))+k2*(V(te,be)))*exp(-pow(V(Y)/yon,2))*exp(I(G1)*V(te,be)/sigmap)*stpp(V(te,be),m)))*(1-(pow((V(Y)-stpp(-V(te,be),m)),6))))}
* capacitor modeling the state
Cint Y 0 1 IC=0.1
* resistor for prevention of convergence problems
Rad Y 0 10G
* Step-like smooth function
.func stpp(x,p)={0.5*(1+(x/sqrt(pow(x,2)+p)))}
.ends K9
*
*Source: 27.	Mladenov, V., S. Kirilov. A Simplified Model of Tantalum Oxide Based 
*Memristor and Application in Memory Crossbars., accepted for presentation and 
*publishing in MOCAST 2021, Greece, http://mocast.physics.auth.gr/.
