*LTSPICE library model of titanium dioxide memristor with Joglekar's window function
*fj=1-(2*x-1)^(2*pp); x is the memristor state variable, pp is a
*positive integer exponent; the nonlinearity of the model increases
*with decreasing the exponent pp
*representing the nonlinear ionic dopant drift for high-level signals
*The model is appropriate for low-frequency and high-amplitude signals
*for analysis of titanium dioxide based memristors and memristor circuits
* terminals - top electrode te, bottom electrode be, and additional
*electrode for measuring the state variable Y
.subckt K2 te be Y
*memristor model parameters
*ron - resistance of the memristor in low-resistance state
*roff - resistance of the memristor for high-resistance state
*k - a constant dependent on the ionic mobility mu, memristor length D and
*the low-resistance ron: k=(mu*ron)/(D^2);
.params ron=100 roff=16e3 k=10e3 Cint=1 pp=3
*capacitor for integration the state variable time derivative
Cint Y 0 1 IC=0.3
*high-valued resistance for preventing convergence problems
Rad Y 0 10G
*memristor state modeling
Gy 0 Y value={(k*V(te,be)*(1/(ron*(V(Y))+roff*(1-V(Y))))*(1-pow((2*(V(Y))-1),(2*pp))))}   
*memristor conductance
G1 te be value={V(te,be)*((1/(ron*(V(Y))+roff*(1-V(Y)))))}
.ends K2
*
*Sources: 1. Joglekar, Y.; Wolf, S. The Elusive Memristor: Properties of Basic 
*Electrical Circuits. Eur. J. Phys. 2009, Vol. 30, pp. 661–675.
*2. Biolek, Z.; Biolek, D.; Biolkova, V. SPICE Model of Memristor with Nonlinear
*Dopant Drift. Radioengineering 2009, 18, pp. 210–214.
