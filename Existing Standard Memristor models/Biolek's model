*titanium dioxide memristor model with Biolek window function
*terminals - top electrode te, bottom electrode be and additional 
*electrode Y for measuring the state variable
.subckt K3 te be Y
*memristor parameters
.params ron=100 roff=16e3 k=10e3 Cint=1 pp=3 m=1e-4
*capacitor for integration the state variable time derivative
Cint Y 0 1 IC=0.3
*high-valued resistance for preventing convergence problems
Rad Y 0 10G
*memristor state modeling
Gy 0 Y value={(k*V(te,be)*(1/(ron*(V(Y))+roff*(1-V(Y))))*(1-pow((V(Y)-stpp(-V(te,be),m)),(2*pp))))}   
*memristor conductance
G1 te be value={V(te,be)*((1/(ron*(V(Y))+roff*(1-V(Y)))))}
* step-like logistic window function
.func stpp(x,p)={0.5*(1+(x/sqrt(pow(x,2)+p)))}
.ends K3
*
*Biolek, Z.; Biolek, D.; Biolkova, V. SPICE Model of Memristor with Nonlinear
*Dopant Drift. Radioengineering 2009, 18, pp. 210â€“214.
