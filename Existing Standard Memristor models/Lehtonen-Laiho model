*titanium dioxide nonlinear Lehtonen-Laiho memristor model 
* with modified Biolek window function
.subckt K5 te be Y
* memristor parameters
.params a=1.1 m=5 mm=1e-4 pp=3 hi=20e-6 beta=60e-6 gama=1.3 alfa=1.4 n=5
*capacitor for integration the state variable time derivative 
Cint Y 0 1 IC=0.3
*high-valued resistance for preventing convergence problems
Rad Y 0 10G
*memristor state modeling
Gy 0 Y value={(a)*(pow(V(te,be),m))*(1-pow((V(Y)-stpp(-V(te,be),mm)),(2*pp)))}
*memristor conductance
G1 te be value={(hi*(exp(gama*(V(te,be)))-1)+(beta*(pow(V(Y),n))*sinh(alfa*(V(te,be)))))}
* step-like logistic window function
.func stpp(x,p)={0.5*(1+(x/sqrt(pow(x,2)+p)))}
.ends K5
*
* Sources: 1. Lehtonen, E.; Laiho, M. CNN using memristors for neighborhood 
* connections. 2010 12th InternationalWorkshop on Cellular Nanoscale Networks 
* and Their Applications (CNNA), IEEE, Berkeley, CA, USA, 
* 3–5 February 2010, DOI: 10.1109/CNNA.2010.5430304, pp. 1–4.
